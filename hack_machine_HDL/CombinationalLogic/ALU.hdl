// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    // if (zx == 1) set x = 0
    Mux16 (a=x, b=false, sel=zx, out=xPartiallyProcessed);
    // if (nx == 1) set x = !x
    Not16 (in=xPartiallyProcessed, out=notx);
    Mux16 (a=xPartiallyProcessed, b=notx, sel=nx, out=xFullyProcessed);
    // if (zy == 1) set y = 0
    Mux16 (a=y, b=false, sel=zy, out=yPartiallyProcessed);
    // if (ny == 1) set y = !y
    Not16 (in=yPartiallyProcessed, out=noty);
    Mux16 (a=yPartiallyProcessed, b=noty, sel=ny, out=yFullyProcessed);

    Add16 (a=xFullyProcessed, b=yFullyProcessed, out=xPLUSy); 
    And16 (a=xFullyProcessed, b=yFullyProcessed, out=xANDy);
    // if (f == 1) set out = x + y ; if (f == 0) set out = x & y
    Mux16 (a=xANDy, b=xPLUSy, sel=f, out=result);

    Not16 (in=result, out=negatedResult);
    // if (no == 1) set out = !out ; if (out < 0) set ng = 1
    Mux16 (a=result, b=negatedResult, sel=no, out=out, out[15]=ng, out[8..15]=firstHalf, out[0..7]=secondHalf);

    Or8Way (in=firstHalf, out=firstOrOutput);
    Or8Way (in=secondHalf, out=secondOrOutput);
    Or (a=firstOrOutput, b=secondOrOutput, out=noZeroBitPresent);
    // if (out == 0) set zr = 1
    Not (in=noZeroBitPresent, out=zr);
}